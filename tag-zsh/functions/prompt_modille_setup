#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_modille_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="%fon ${_branch_color}"$'\ue725'" %b%f%c%u%F{1}"$'\uf059'" %f"
  else
    branch_format="%fon ${_branch_color}"$'\ue725'" %b%f%c%u "
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_modille_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_modille_precmd

  # Base16 colors
  # Use ./colortest in github.com/chriskempson/base16-shell to view them
  # Note that some themes (e.g. dracula) swap yellow and green
  _color00_black="%F{0}"
  _color01_red="%F{1}"
  _color02_green="%F{2}"
  _color03_yellow="%F{3}"
  _color04_blue="%F{4}"
  _color05_magenta="%F{5}"
  _color06_cyan="%F{6}"
  _color07_white="%F{7}"
  _color08_bright_black="%F{8}"
  _color09_bright_red="%F{9}"
  _color10_bright_green="%F{10}"
  _color11_bright_yellow="%F{11}"
  _color12_bright_blue="%F{12}"
  _color13_bright_magenta="%F{13}"
  _color14_bright_cyan="%F{14}"
  _color15_bright_white="%F{15}"
  _color16_alt1="%F{16}"
  _color17_alt2="%F{17}"
  _color18_alt3="%F{18}"
  _color19_alt4="%F{19}"
  _color20_alt5="%F{20}"
  _color21_alt6="%F{21}"

  # Configure different prompt pieces
  _action_color="${_color01_red}"
  _branch_color="${_color14_bright_cyan}"
  _dot_unstaged_color="${_color03_yellow}"
  _dot_staged_color="${_color02_green}"
  _pwd_color="${_color14_bright_cyan}"
  _return_status_ok_color="${_color02_green}"
  _return_status_error_color="${_color01_red}"

  # Return status
  # TODO Might be nice to also print the return code on error
  local ret_status="%(?:${_return_status_ok_color}"$'\uf00c'":${_return_status_error_color}"$'\uf00d'"%s)"

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="%fon ${_branch_color}"$'\ue725'"%b%f%c%u"
  local action_format="%f${_action_color}"$'\uf0ad'" %a in progress %f"
  local unstaged_format="${_dot_unstaged_color}"$'\uf111'" %f"
  local staged_format="${_dot_staged_color}"$'\uf058'" %f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT=$'\u256D'$'\U2500'" ${ret_status} "'${vcs_info_msg_0_}'"%fin ${_pwd_color}"$'\uf413'" %~ %fat ${_color04_blue}"$'\ufd31'' $ZSH_KUBECTL_PROMPT'"%f"$'\n'$'\u2570'$'\U2500 '"$ %b"
  RPROMPT=''
}

prompt_modille_setup "$@"
