#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_modille_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_steeef_colors[7]}on ${_prompt_steeef_colors[8]}%b%f%u%c${_prompt_steeef_colors[4]}●%f "
  else
    branch_format="${_prompt_steeef_colors[7]}on ${_prompt_steeef_colors[8]}%b%f%u%c "
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_modille_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_modille_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_steeef_colors=(
      "%F{81}"  # 1 Turquoise
      "%F{166}" # 2 Orange
      "%F{135}" # 3 Purple
      "%F{161}" # 4 Hotpink
      "%F{118}" # 5 Limegreen
      "%F{154}" # 6 lime
      "%F{015}" # 7 snow
      "%F{205}" # 8 pink
    )
  else
    _prompt_steeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Return status
  local ret_status="%(?:${_prompt_steeef_colors[6]}✔:%F{red}×%s)"

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_steeef_colors[7]}aon ${_prompt_steeef_colors[1]}%b%f%u%c "
  local action_format="${_prompt_steeef_colors[7]}bon ${_prompt_steeef_colors[5]}%a%f "
  local unstaged_format="${_prompt_steeef_colors[2]}●%f"
  local staged_format="${_prompt_steeef_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT="%B${ret_status} "'${vcs_info_msg_0_}'"${_prompt_steeef_colors[7]}in ${_prompt_steeef_colors[8]}%~%f $ %b"
  RPROMPT=''
}

prompt_modille_setup "$@"
